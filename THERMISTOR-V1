//Original file name: thermistor_temp_intervalresponse
// which analog pin to connect
#define THERMISTORPIN A0         
// resistance at 25 degrees C
#define THERMISTORNOMINAL 10000      
// temp. for nominal resistance (almost always 25 C)
#define TEMPERATURENOMINAL 25   
// how many samples to take and average, more takes longer
// but is more 'smooth'
#define NUMSAMPLES 5
// The beta coefficient of the thermistor (usually 3000-4000)
#define BCOEFFICIENT 3950
// the value of the 'other' resistor
#define SERIESRESISTOR 10000    
 
uint16_t samples[NUMSAMPLES];
float highT;
float lowT;
 
void setup(void) {
  Serial.begin(9600);
  //analogReference(EXTERNAL);
  Serial.println("Maximum batch temperature: "); //user enters high temperature
  while (Serial.available()==0){} //testing if user has inputed value (0 when nothing put in)
  highT = Serial.parseFloat();
  Serial.println(highT);  
  
  Serial.println("Minimum batch temperature: "); //user enters low temp
  while (Serial.available()==0){}
  lowT = Serial.parseFloat();
  Serial.println(lowT);
}
 
void loop(void) {
  uint8_t i;
  float average;
 
  // take N samples in a row, with a slight delay
  for (i=0; i< NUMSAMPLES; i++) {
   samples[i] = analogRead(THERMISTORPIN);
   delay(10);
  }
 
  // average all the samples out
  average = 0;
  for (i=0; i< NUMSAMPLES; i++) {
     average += samples[i];
  }
  average /= NUMSAMPLES;
 
  Serial.print("Average analog reading "); 
  Serial.println(average);
 
  // convert the value to resistance
  average = 1023 / average - 1;
  average = SERIESRESISTOR / average;
  Serial.print("Thermistor resistance "); 
  Serial.println(average);
 
  float steinhart;
  steinhart = average / THERMISTORNOMINAL;     // (R/Ro)
  steinhart = log(steinhart);                  // ln(R/Ro)
  steinhart /= BCOEFFICIENT;                   // 1/B * ln(R/Ro)
  steinhart += 1.0 / (TEMPERATURENOMINAL + 273.15); // + (1/To)
  steinhart = 1.0 / steinhart;                 // Invert
  steinhart -= 273.15;                         // convert to C
 
  Serial.print("Temperature "); 
  Serial.print(steinhart);
  Serial.println(" *C");
  if (steinhart > highT){
    Serial.println("Temp is too high");
    //digitalWrite(13, HIGH);    //set pin 13 on   
    //delay(800);   //leave on for 800 ms
    //digitalWrite(13, LOW); //set pin 13 off (blink LED ?) 
  }
  if (steinhart < lowT){
    Serial.println("Temp is too low");
    //digitalWrite(13, HIGH);    //set pin 13 on   
    //delay(800);   //leave on for 800 ms
    //digitalWrite(13, LOW); //set pin 13 off (blink LED ?)
  }
  delay(1000);

  }
